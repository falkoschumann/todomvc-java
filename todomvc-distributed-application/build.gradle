import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'todomvc.java-application-conventions'
  id 'com.gluonhq.client-gradle-plugin'
  id 'org.beryx.jlink'
}

dependencies {
  implementation project(':todomvc-frontend')
  implementation 'com.google.code.gson:gson:2.8.6'
}

def year = LocalDate.now().year
def copyright = "Copyright (c) ${year} Falko Schumann"

processResources {
  filesMatching('**/*.properties') {
    expand(title: 'Todos', version: rootProject.version, copyright: copyright)
  }
}

application {
  mainModule = 'de.muspellheim.todomvc.distributed'
  mainClassName = 'de.muspellheim.todomvc.distributed.App'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    launcher {
      args = ['--useSystemMenuBar']
    }
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'TodoMVC'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', copyright,
        '--mac-sign',
        '--mac-signing-key-user-name', project.findProperty('macSigning.keyUserName')
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'TodoMVC'
      imageOptions = [
        '--copyright', copyright,
      ]
      installerName = 'TodoMVC'
      installerType = 'msi'
      installerOptions = [
        '--copyright', copyright,
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', '1f42ed58-4e2b-49e8-bc39-f333396ff8b6'
      ]
    }
  }
}

gluonClient {
  resourcesList = ['app.properties']
  reflectionList = [
    'de.muspellheim.todomvc.contract.data.Todo',
    'de.muspellheim.messages.HttpCommandStatus',
    'de.muspellheim.todomvc.contract.messages.commands.ClearCompletedCommand',
    'de.muspellheim.todomvc.contract.messages.commands.DestroyCommand',
    'de.muspellheim.todomvc.contract.messages.commands.EditCommand',
    'de.muspellheim.todomvc.contract.messages.commands.NewTodoCommand',
    'de.muspellheim.todomvc.contract.messages.commands.ToggleAllCommand',
    'de.muspellheim.todomvc.contract.messages.commands.ToggleCommand',
    'de.muspellheim.todomvc.contract.messages.queries.TodosQuery',
    'de.muspellheim.todomvc.contract.messages.queries.TodosQueryResult',
    'de.muspellheim.todomvc.frontend.TodosViewController',
    'de.muspellheim.todomvc.frontend.InfoViewController',
    'javafx.geometry.Insets',
    'javafx.scene.control.Button',
    'javafx.scene.control.ChoiceBox',
    'javafx.scene.control.Label',
    'javafx.scene.control.ListView',
    'javafx.scene.control.MenuButton',
    'javafx.scene.control.MenuItem',
    'javafx.scene.control.Separator',
    'javafx.scene.control.TextField',
    'javafx.scene.control.ToggleButton',
    'javafx.scene.control.Tooltip',
    'javafx.scene.image.Image',
    'javafx.scene.image.ImageView',
    'javafx.scene.layout.HBox',
    'javafx.scene.layout.VBox',
    'javafx.scene.text.Text',
    'javafx.scene.text.TextFlow',
    'javafx.geometry.Insets',
    'javafx.scene.control.Label',
    'javafx.scene.image.Image',
    'javafx.scene.image.ImageView',
    'javafx.scene.layout.VBox',
  ]
}
