plugins {
  id 'base'
  id 'com.diffplug.spotless'
  id 'com.github.node-gradle.node'
}

node {
  download = true
  version = "12.20.1"
}

task npmBuild(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'build']
  inputs.dir(project.fileTree('src').exclude('**/*.test.ts*'))
  inputs.dir('public')
  inputs.dir('node_modules')
  inputs.file('tsconfig.json')
  outputs.dir('build/www')
}
assemble.dependsOn(npmBuild)

task npmStart(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['start']
}

task npmStorybook(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'storybook']
}

task npmTest(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'test']
  args = ['--', '--watchAll=false']
  inputs.dir('src')
  inputs.dir('node_modules')
  inputs.file('tsconfig.json')
}
check.dependsOn(npmTest)

task npmPrettierCheck(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'prettier:check']
}
check.dependsOn(npmPrettierCheck)

task npmPrettierWrite(type: NpmTask, dependsOn: npmInstall) {
  npmCommand = ['run', 'prettier:write']
}

// TODO Spotless will Prettier mit `npm install` installieren, bevor NodeJS/NPM installiert wurden
//   Siehe https://github.com/diffplug/spotless/issues/728
//spotless {
//  format 'javascript', {
//    target 'src/**/*.js', 'src/**/*.jsx', 'src/**/*.ts', 'src/**/*.tsx', 'src/**/*.json', 'src/**/*.css', 'src/**/*.scss', 'src/**/*.md'
//    //prettier().npmExecutable(nodeSetup.nodeDir.get().dir('./bin/npm')).configFile("${projectDir}/.prettierrc.json")
//    prettier().configFile("${projectDir}/.prettierrc.json")
//  }
//}
